package Libraries.Development.Environment.Studio

use Libraries.Game.Game
use Libraries.Interface.Controls.ScrollPane
use Libraries.Development.Environment.Projects.ProjectTree
use Libraries.Interface.Controls.TabPane
use Libraries.Interface.Controls.TextBox
use Libraries.Interface.Item2D
use Libraries.Game.Graphics.Color
use Libraries.Development.Environment.Projects.ProjectManager
use Libraries.Development.Environment.Studio.Updates.Updater
use Libraries.Development.Environment.Configuration.Options
use Libraries.Development.Environment.Resources
use Libraries.Development.Environment.Studio.Behaviors.Behaviors
use Libraries.Game.Layer2D
use Libraries.Containers.Array
use Libraries.System.File
use Libraries.Interface.Layouts.Layout
use Libraries.Interface.Controls.Tab
use Libraries.Game.Graphics.Texture
use Libraries.Game.GameStateManager
use Libraries.Game.Graphics.GraphicsManager
use Libraries.Game.DesktopConfiguration
use Libraries.Interface.Item
use Libraries.Interface.Events.FocusListener
use Libraries.Interface.Events.FocusEvent
use Libraries.Game.InputTable
use Libraries.Development.Environment.Studio.Interface.KeyMap
use Libraries.Containers.Iterator
use Libraries.Development.Environment.Studio.Behaviors.ScanLibraryBehavior
use Libraries.Development.Environment.Studio.Build.BuildManager
use Libraries.Concurrency.ThreadRunner
use Libraries.System.SystemHelper
use Libraries.Interface.Layouts.LayoutProperties
use Libraries.Interface.Controls.Control
use Libraries.Interface.Layouts.FlowLayout
use Libraries.Interface.Layouts.ManualLayout
use Libraries.Game.InputMonitor
use Libraries.Interface.Events.KeyboardEvent
use Libraries.Development.Environment.Studio.Behaviors.BuildLoop
use Libraries.Game.Graphics.FramesPerSecondLabel
use Libraries.Interface.Controls.Dialog
use Libraries.Game.Graphics.Label
use Libraries.Development.Environment.Studio.Interface.SplashScreen
use Libraries.Development.Environment.Studio.Build.ScanListener
use Libraries.Development.Environment.Studio.StudioFocusManager
use Libraries.Sound.Audio
use Libraries.Interface.Events.ResizeListener
use Libraries.Interface.Events.ResizeEvent
use Libraries.Containers.HashTable
use Libraries.System.Console
use Libraries.Development.Environment.Studio.Behaviors.CodeCompletionInputTable
use Libraries.Interface.Behaviors.Controls.MenuRootDownBehavior
use Libraries.Interface.Behaviors.Controls.MenuRootUpBehavior
use Libraries.Interface.Behaviors.Controls.MenuRootActivateBehavior
use Libraries.Interface.Behaviors.Controls.MenuRootCloseBehavior
use Libraries.Game.InputSet
use Libraries.Development.Environment.Studio.Behaviors.OpenSaveBeforeClosingDialog
use Libraries.Development.Environment.Studio.Interface.SaveBeforeClosingDialog
use Libraries.Development.Environment.Studio.Behaviors.CloseCodeCompletionBehavior
use Libraries.Interface.Behaviors.Behavior
use Libraries.Development.Environment.Studio.Behaviors.QuorumStudioBehavior
use Libraries.Interface.Controls.Tree
use Libraries.Interface.Selections.TabPaneSelection
use Libraries.Development.Environment.Studio.Behaviors.PassthroughInputTable
use Libraries.Development.Environment.Studio.Mobile.AppleDevicesReader
use Libraries.System.Logger
use Libraries.System.DateTime
use Libraries.Interface.Controls.Scenes.Items.EditorItem
use Libraries.Interface.Controls.Scenes.SceneConstants
use Libraries.Interface.Controls.Scenes.SceneEditorInputTable
use Libraries.Interface.Controls.Scenes.SceneTree
use Libraries.Interface.Controls.Scenes.ScenePalette

class QuorumStudio is Game, FocusListener, ScanListener, ResizeListener
    constant integer MAJOR_VERSION = 4
    constant integer MINOR_VERSION = 0
    constant integer BUILD_VERSION = 2
    constant text NAME = "Quorum Studio " + MAJOR_VERSION + "." + MINOR_VERSION + "." + BUILD_VERSION
    public integer SAVE_CHANGES = 0
    public integer DISCARD_CHANGES = 1
    public integer CANCEL_EXIT = 2
    integer DEFAULT_WINDOW_WIDTH = 2500
    integer DEFAULT_WINDOW_HEIGHT = 1500
    TabPane treeTabPane
    Control treeNavigatorPane
    ProjectTree projectTree
    SceneTree sceneTree
    ScenePalette scenePalette
    CodeEditorTabPane codeTabs
    OutputEditorTabPane outputWindow
    Control mainContentPane
    // inputOutputPane contains both the code editor and the output area.
    Control inputOutputPane
    Color color
    
    //Color background
    ProjectManager manager
    Updater updater
    Toolbar toolbar
    Menu menu
    number tempXOffset = 0

    Options options
    File projectsFolder
    Resources resources
    Behaviors behaviors
    OpenSaveBeforeClosingDialog openSaveBeforeClosingBehavior
    Array<Item> focusList
    HashTable<text, integer> windowsList
    integer focus = -1

    Item currentFocus = undefined
    Item previousFocus = undefined
    SystemHelper helper
    BuildLoop buildLoop
    SplashScreen splashScreen = undefined
    boolean splashScreenHide = false
    integer exitStatus = SAVE_CHANGES
    Logger logger

    // Used to unilaterally enable/disable 3D scene features during development
    public boolean enabledScenes3D = true

    action GetDisplayName returns text
        return NAME
    end

    action IsNewer(integer newMajorVersion, integer newMinorVersion, integer newBuildVersion) returns boolean
        integer major = GetMajorVersion()
        integer minor = GetMinorVersion()
        integer build = GetBuildVersion()

        if newMajorVersion > major
            return true
        elseif newMajorVersion = major and newMinorVersion > minor
            return true
        elseif newMajorVersion = major and newMinorVersion = minor and 
            newBuildVersion > build
            return true
        end
        
        return false
    end
    action GetVersion returns text
        return "" + MAJOR_VERSION + "." + MINOR_VERSION + "." + BUILD_VERSION
    end

    action GetMajorVersion returns integer
        return MAJOR_VERSION
    end

    action GetMinorVersion returns integer
        return MINOR_VERSION
    end

    action GetBuildVersion returns integer
        return BUILD_VERSION
    end

    action Start
        DateTime time
        logger:Log("Starting Quorum Studio")
        DesktopConfiguration config = GetDesktopConfiguration()
        config:title = NAME
        StartGame()
    end

    action SetExitStatus (integer status)
        exitStatus = status
    end

    action GetExitStatus returns integer
        return exitStatus
    end

    action ResetExitFlag
        openSaveBeforeClosingBehavior:SetExitFlag(true)
    end

    action GetExitFlag returns boolean
        return openSaveBeforeClosingBehavior:GetExitFlag()
    end

    action OnExit returns boolean
        /* Checks to see if there are unsaved changes and if this is the first prompt for the unsaved
        changes. If yes, then the Save Changes Before Closing Dialog will pop up and the user can choose: 
        SAVE ALL changes in which QuorumStudio calls exit again (exitFlag = false, so the dialog 
        does not prompt the user again.
        DISCARD ALL changes calling exit again and saving the state of quorum studio without saving any 
        changes to the files themselves.
        or CANCEL EXIT in which the dialog is closed and the exitFlag is reset to true from the cancel exit behavior
        so that the next time the user goes to exit QuroumStudio they will be prompted with the dialog again.*/
        if codeTabs:GetDirtyTabsCount() > 0 and (openSaveBeforeClosingBehavior:GetExitFlag()) 
            openSaveBeforeClosingBehavior:Run()
            SaveBeforeClosingDialog dialog = openSaveBeforeClosingBehavior:GetDialog()
            return false
        end
        if exitStatus = CANCEL_EXIT
            return false
        end
        
        ThreadRunner buildThreadRunner = behaviors:GetBuildThreadRunner()
        ThreadRunner debuggerRunner = behaviors:GetDebuggerRunner()
        ThreadRunner indexThread = behaviors:GetBuildIndexingThread()
        ThreadRunner gitRunner = behaviors:GetGitRunner()
        ThreadRunner networkRunner = behaviors:GetNetworkThreadRunner()
        buildLoop:SetFinished(true)
        buildThreadRunner:ShutDown()
        debuggerRunner:ShutDown()
        indexThread:ShutDown()
        gitRunner:ShutDown()
        networkRunner:ShutDown()

        BuildManager value = GetBuildManager()
        value:StopAll()
        if exitStatus = SAVE_CHANGES
            Save()
        end
        logger:Log("Closing Quorum Studio")
        logger:Close()
        return true
    end

    action GetOptions returns Options
        return options
    end

    action GetBehaviors returns Behaviors
        return behaviors
    end

    action GetBuildManager returns BuildManager
        return behaviors:GetBuildManager()
    end     

    action GetCurrentFocus returns Item
        return currentFocus
    end

    action GetPreviousFocus returns Item
        return previousFocus
    end

    action GainedFocus(FocusEvent event)
        currentFocus = event:GetNewFocus()
        previousFocus = event:GetLastFocus()

        if currentFocus is EditorItem
            // If the focus was an EditorItem, it must be from the scene editor.
            focus = windowsList:GetValue(codeTabs:GetName())

        //Bubble up the parents until you find which window you're in
        //then set the window focus cycle to move there
        elseif currentFocus is Item2D
            Item2D newFocus = cast(Item2D, currentFocus)
            if newFocus:GetParent() not= undefined
                boolean notFound = true
                repeat while notFound and newFocus:GetParent() not= undefined
                    if windowsList:HasKey(newFocus:GetParent():GetName())
                        focus = windowsList:GetValue(newFocus:GetParent():GetName())
                        notFound = false
                    end
                    newFocus = newFocus:GetParent()
                end
            end
        end
    end

    action LostFocus(FocusEvent event)

    end

    action GetOutputPane returns OutputEditorTabPane
        return outputWindow
    end

    action GetProjectManager returns ProjectManager
        return manager
    end

    action GetProjectTree returns ProjectTree
        return projectTree
    end

    action GetSceneTree returns SceneTree
        return sceneTree
    end

    action GetScenePalette returns ScenePalette
        return scenePalette
    end

    action GetToolbar returns Toolbar
        return toolbar
    end

    action GetTabPane returns CodeEditorTabPane
        return codeTabs
    end

    action GetProjectsFolder returns File
        return projectsFolder
    end

    action NextFocus returns Item
        focus = focus + 1
        if focus >= focusList:GetSize()
            focus = 0
        end

        return focusList:Get(focus)
    end

    action PreviousFocus returns Item
        focus = focus - 1
        if focus < 0
            focus = focusList:GetSize() - 1
        end

        return focusList:Get(focus)
    end

    action GetFocusSelection returns integer
        return focus
    end

    /*
    This action returns the current tree which is active in the tree tab pane.
    For example, if the ProjectTree is visible, the ProjectTree will be returned.
    If the SceneTree is currently visible in the active tab, it will be returned instead.
    */
    action GetActiveTree returns Tree
        TabPaneSelection selection = treeTabPane:GetSelection()
        Tab tab = selection:GetTab()
        Item focusTarget = tab:GetFocusTarget()
        if focusTarget is Tree
            return cast(Tree, focusTarget)
        end
        return undefined
    end

    /* When Scanning starts, because it is in another thread anyway, check for available devices on bootup. */
    action Started
        AppleDevicesReader reader
    end

    action Stopped
        splashScreenHide = true
    end

    action SizeChanged(ResizeEvent event)
        integer height = event:GetHeight()
        integer width = event:GetWidth()
        options:SetWidth(GetScreenWidth())
        options:SetHeight(GetScreenHeight())
    end

    action CreateGame
        resources:SetOptions(options)

        AddResizeListener(me)
        StudioFocusManager focusManager
        SetFocusManager(focusManager)
        FlowLayout flowLayout
        SetLayout(flowLayout)

        options:SetMainApplication(me)
        GameStateManager gameStateManager
        GraphicsManager graphics = gameStateManager:GetGameGraphics()
        graphics:ClearScreenColor(101.0/255.0, 101.0/255.0, 101.0/255.0, 1.0)
        SetupResources()

        Layer2D layer = me:GetCurrentLayer2D()
        layer:SetAutomaticResizing(true)

        BuildManager builds = behaviors:GetBuildManager()
        builds:SetOptions(options)
        builds:SetCodeEditorTabPane(codeTabs)
        builds:SetOutputEditorTabPane(outputWindow)
        builds:SetProjectManager(manager)

        Load()
        SetupInterface()
        integer width = options:GetWidth()
        integer height = options:GetHeight()
        number size = options:GetInterfaceScale()
        SetInterfaceScale(size)
        SetScreenSize(width, height)
        mainContentPane:Resize()

        SetApplicationIcon(resources:GetApplicationIcon())

        SplashScreen splash
        splashScreen = splash
        splashScreen:Initialize()
        splashScreen:SetLibrary(GetBuildManager():GetLibrary())
        splashScreen:Show()

        SetupFocus()
        SetupInterfaceScaling()
        SetupInputTables()
        
        

        //now that we're all setup, tell the library scanner to run
        ScanLibraryBehavior scanner = behaviors:GetScanLibraryBehavior()
        scanner:Add(me)
        BuildManager builder = behaviors:GetBuildManager()
        ThreadRunner buildThreadRunner = behaviors:GetBuildThreadRunner()
        ThreadRunner indexThread = behaviors:GetBuildIndexingThread()
        buildThreadRunner:Add(scanner)
        buildLoop:SetBuildManager(builder)

        indexThread:Add(buildLoop)
        FocusWindow()

        if options:IsShowingFramesPerSecondCounter()
            FramesPerSecondLabel fps
            fps:SetSize(50)

            //get the current Layer2D and turn off depth buffer sorting for Quorum Studio
            Layer2D currentLayer2 = GetCurrentLayer2D()
            SetColliding(false)

            Layer2D fpsLayerOfAwesome
            fpsLayerOfAwesome:Add(fps)
            AddLayer(fpsLayerOfAwesome)
            fpsLayerOfAwesome:SetDepthBufferSorting(false)
            fpsLayerOfAwesome:SetColliding(false)
        end 
    end

    action SetupFocus
    /*
        The order the items are added to the focusList and the value
        associated with them in the hashTable affect the way in which
        focus cycles between the main windows
    */
        focusList:Add(toolbar)
        focusList:Add(projectTree)
        focusList:Add(codeTabs)
        focusList:Add(outputWindow)
        windowsList:Add(toolbar:GetName(), 0)
        windowsList:Add(projectTree:GetName(), 1)
        windowsList:Add(codeTabs:GetName(), 2)
        windowsList:Add(outputWindow:GetName(), 3)

        KeyMap keyMap = resources:GetKeyMap()
        
        AddFocusListener(me)
    end

    action SetupInterfaceScaling
        InputTable table = GetDefaultInputTable()
        KeyMap keyMap = resources:GetKeyMap()

        //table:Add(keyMap:GetScaleInterfaceUpKey(), behaviors:GetScaleInterfaceUpBehavior())
        //table:Add(keyMap:GetScaleInterfaceDownKey(), behaviors:GetScaleInterfaceDownBehavior())
    end

    action GetResources returns Resources
        return resources
    end

    action SetupResources
        manager:Add(projectTree)
        manager:Add(options)

        //load shared resources for all GUI components
        resources:Load()
        projectTree:SetResources(resources)
        projectTree:SetMainApplication(me)
        toolbar:SetResources(resources)
        menu:SetResources(resources)
        codeTabs:SetResources(resources)
        outputWindow:SetResources(resources)

        projectTree:SetProjectManager(manager)
        AddSelectionListener(projectTree)
        
        toolbar:SetBehaviors(behaviors)
        menu:SetBehaviors(behaviors)
        outputWindow:SetBehaviors(behaviors)
        codeTabs:SetBehaviors(behaviors)
        behaviors:SetMainApplication(me)
    end

    action SetupInterface
        menu:Setup()
        toolbar:Setup()

        CreateMainContentPane()
        Add(menu)
        Add(toolbar)
        Add(mainContentPane)
    end

    action SetupInputTables
        KeyboardEvent keys

        CodeCompletionInputTable codeCompletionTable
        PassthroughInputTable passthrough
        AddInputTable(passthrough)

        MenuRootDownBehavior menuDownBehavior
        MenuRootUpBehavior menuUpBehavior
        MenuRootActivateBehavior menuRunBehavior
        CloseCodeCompletionBehavior menuCloseBehavior

        // Used only to prevent certain inputs from propagating downward
        Behavior emptyBehavior

        QuorumStudioBehavior codeCompletionBehavior = behaviors:GetCodeCompletionBehavior()

        InputSet downArrow
        InputSet upArrow
        InputSet enterSet
        InputSet escapeSet
        InputSet spaceSet
        downArrow:SetKeyboardInput(keys:DOWN)
        upArrow:SetKeyboardInput(keys:UP)
        enterSet:SetKeyboardInput(keys:ENTER)
        escapeSet:SetKeyboardInput(keys:ESCAPE)
        spaceSet:SetKeyboardInput(keys:SPACE)

        InputSet releaseDownArrow
        InputSet releaseUpArrow
        releaseDownArrow:SetKeyboardInput(keys:DOWN)
        releaseDownArrow:SetInputTrigger(releaseDownArrow:FINISH)
        releaseUpArrow:SetKeyboardInput(keys:UP)
        releaseUpArrow:SetInputTrigger(releaseUpArrow:FINISH)

        codeCompletionTable:Add(downArrow, menuDownBehavior)
        codeCompletionTable:Add(upArrow, menuUpBehavior)
        codeCompletionTable:Add(enterSet, menuRunBehavior)
        codeCompletionTable:Add(escapeSet, menuCloseBehavior)
        codeCompletionTable:Add(spaceSet, menuCloseBehavior)
        codeCompletionTable:Add(releaseDownArrow, emptyBehavior)
        codeCompletionTable:Add(releaseUpArrow, emptyBehavior)

        AddInputTable(codeCompletionTable)
        //SetupVisualEditorInput()
    end

    action Load
        //load the user's settings
        options:Read()

        me:projectsFolder = options:GetProjectsFolder()
        if not projectsFolder:Exists()
            projectsFolder:CreateDirectories()
        end

        Array<text> projects = options:GetProjects()

        i = 0
        repeat while i < projects:GetSize()
            text value = projects:Get(i)
            File file
            file:SetAbsolutePath(value)
            manager:Open(file)
            i = i + 1
        end

        Iterator<File> iterator = options:GetFilesOpenOnStartup()
        repeat while iterator:HasNext()
            File file = iterator:Next()
            codeTabs:OpenNewTab(file)
        end
        codeTabs:Display(0)

        Console console
        Array<text> flags = console:GetConsoleArguments()
        if flags not= undefined and flags:GetSize() > 0
            i = 0
            repeat while i < flags:GetSize()
                text value = flags:Get(i)
                check
                File file
                file:SetAbsolutePath(value)
                if file:Exists()
                    manager:Open(file)
                end
                detect e //if the user passed junk, just ignore it
                end
                i = i + 1
            end
        end
    end

    action Save
        options:SetWidth(GetScreenWidth())
        options:SetHeight(GetScreenHeight())
        options:SetInterfaceScale(GetInterfaceScale())
        manager:SaveOpenProjects()
        options:Write()
        File mapFile = options:GetKeyMapFile()
        KeyMap map = resources:GetKeyMap()
        if map not= undefined
            map:Save(mapFile)
        end

        codeTabs:SaveDirtyTabs()
    end

    action GetMenu returns Menu
        return menu
    end

    private action CreateMainContentPane
        LayoutProperties layoutProperties = mainContentPane:GetDefaultLayoutProperties()
        layoutProperties:SetVerticalLayoutMode(layoutProperties:FILL)
        layoutProperties:SetPercentageWidth(1)
        layoutProperties:SetTopPadding(5)

        FlowLayout layout
        mainContentPane:SetLayout(layout)
        mainContentPane:SetName("Main Content")

        CreateProjectsWindow()
        CreateCodeEditor()
        CreateOutputWindow()

        LayoutProperties inputOutputProperties = inputOutputPane:GetDefaultLayoutProperties()
        inputOutputProperties:SetHorizontalLayoutMode(inputOutputProperties:FILL)
        inputOutputProperties:SetVerticalLayoutMode(inputOutputProperties:FILL)
        FlowLayout inputOutputLayout
        inputOutputPane:SetLayout(inputOutputLayout)
        inputOutputPane:SetName("Input Output")

        mainContentPane:Add(treeNavigatorPane)
        inputOutputPane:Add(codeTabs)
        inputOutputPane:Add(outputWindow)
        mainContentPane:Add(inputOutputPane)
    end

    action CreateCodeEditor
    end

    action CreateProjectsWindow()
        Tab projectsTab
        projectsTab:SetName("Projects")
        projectsTab:DisplayCloseButton(false)
        ScrollPane projectPane

        LayoutProperties treeNavigatorLayout = treeNavigatorPane:GetDefaultLayoutProperties()
        treeNavigatorLayout:SetVerticalLayoutMode(treeNavigatorLayout:FILL)
        treeNavigatorLayout:SetHorizontalLayoutMode(treeNavigatorLayout:STANDARD)
        treeNavigatorLayout:SetPixelWidth(300)
        FlowLayout flow
        treeNavigatorPane:SetLayout(flow)

        LayoutProperties treeTabLayout = treeTabPane:GetDefaultLayoutProperties()
        treeTabLayout:SetHorizontalLayoutMode(treeTabLayout:FILL)
        treeTabLayout:SetVerticalLayoutMode(treeTabLayout:FILL)
        
        FlowLayout layout
        treeTabPane:SetViewAreaLayout(layout)

        //projectPane:SetBackgroundColor(resources:GetBackgroundColor())
        projectPane:SetName("Project Pane")
        projectPane:SetPercentageWidth(1.0)
        projectPane:SetPercentageHeight(1.0)
        projectPane:Add(projectTree)

        projectsTab:SetRelatedItem(projectPane)
        projectsTab:SetFocusTarget(projectTree)
        treeTabPane:Add(projectsTab)
        treeNavigatorPane:Add(treeTabPane)

        Tab sceneTab
        sceneTab:SetName("Scene")
        sceneTab:DisplayCloseButton(false)
        ScrollPane scenePane

        //scenePane:SetBackgroundColor(resources:GetBackgroundColor())
        scenePane:SetName("Scene Pane")
        scenePane:SetPercentageWidth(1.0)
        scenePane:SetPercentageHeight(1.0)
        scenePane:Add(sceneTree)
        sceneTree:SetSceneEditor(undefined)

        sceneTab:SetRelatedItem(scenePane)
        sceneTab:SetFocusTarget(sceneTree)
        treeTabPane:Add(sceneTab)

        Tab paletteTab
        paletteTab:SetName("Palette")
        paletteTab:DisplayCloseButton(false)
        ScrollPane palettePane

        //palettePane:SetBackgroundColor(resources:GetBackgroundColor())
        palettePane:SetName("Palette Pane")
        palettePane:SetPercentageWidth(1.0)
        palettePane:SetPercentageHeight(1.0)
        palettePane:Add(scenePalette)
        scenePalette:SetDefaultCubeThumbnail(resources:CUBE_THUMBNAIL)
        scenePalette:SetDefaultSphereThumbnail(resources:SPHERE_THUMBNAIL)
        scenePalette:SetDefaultCylinderThumbnail(resources:CYLINDER_THUMBNAIL)
        scenePalette:SetDefaultPointLightThumbnail(resources:LIGHT_THUMBNAIL)
        scenePalette:SetDefaultAudio3DThumbnail(resources:AUDIO_THUMBNAIL)
        scenePalette:SetDefaultCustomModelThumbnail(resources:CUSTOM_MODEL_THUMBNAIL)
        scenePalette:SetDefaultCustomCubeThumbnail(resources:CUSTOM_CUBE_THUMBNAIL)
        scenePalette:SetDefaultCustomSphereThumbnail(resources:CUSTOM_SPHERE_THUMBNAIL)
        scenePalette:SetDefaultCustomCylinderThumbnail(resources:CUSTOM_CYLINDER_THUMBNAIL)
        scenePalette:SetPointLightTexture(resources:GetLightTexture())
        scenePalette:SetAudio3DTexture(resources:GetAudioTexture())
        scenePalette:LoadDefaultAssets()

        paletteTab:SetRelatedItem(palettePane)
        paletteTab:SetFocusTarget(scenePalette)
        treeTabPane:Add(paletteTab)

        treeTabPane:Select(projectsTab)
    end

    action GetProjectsTabPane returns TabPane
        return treeTabPane
    end

    private action RemoveHiddenFiles(Array<File> directory)
        integer i = 0
        repeat while i < directory:GetSize()
            if(directory:Get(i):IsHidden())
                directory:Remove(directory:Get(i))
            end
            i = i + 1
        end
    end

    action CreateOutputWindow()
        outputWindow:Setup()
    end

    action LoadUpdates
        File defaultUpdateFile
        defaultUpdateFile:SetPath("Configuration/Updates.json")
        updater:Load(defaultUpdateFile)

        Downloader download
        download:SetPath("https://quorumlanguage.com/studio/live/Configuration/Updates.json")
        boolean value = download:Exists()

        File downloadHere
        downloadHere:SetPath("Updates/Configuration/Updates.json")
        File mom = downloadHere:GetParentDirectory()
        if not mom:Exists()
            mom:CreateDirectories()
        end

        download:SetFile(downloadHere)
        download:Download()
        
        Updater newUpdate
        newUpdate:Load(downloadHere)
        updater:CheckForNewerVersion(newUpdate)
        if updater:HasUpdates()
            updater:DownloadModules()
        end
    end

    action CheckForUpdates
        //first ping the server and see if the files are there.
        
        //get the file --- for now let's get one from testing
        File new
        new:SetPath("Tests/UpdatesTests/UpdateNewVersion.json")

        Updater up
        up:Load(new)

        updater:CheckForNewerVersion(up)
    end

    action IsSupportedFile(File file) returns boolean
        text extension = file:GetFileExtension()
        if extension = "quorum"
            return true
        elseif extension = "qs"
            return true
        elseif extension = "csv"
            return true
        else
            //other supported extensions would be here
        end
        return false
    end

    action GetStudioFocusManager returns StudioFocusManager
        return cast(StudioFocusManager, GetFocusManager())
    end

    action Update(number seconds)
        if splashScreenHide 
            splashScreen:Hide()
            splashScreenHide = false
            //The projects have to loaded to correctly open all the subtrees
            //so this is happening here
            projectTree:LoadExpandState(options:GetTreeState())
            projectTree:LoadSelection(options:GetTreeSelect())
            SetFocus(projectTree)
        end
    end
end
